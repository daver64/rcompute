#version 430
layout(local_size_x = 8, local_size_y = 8) in;

layout(std430, binding = 0) buffer MatrixA {
    float A[];
};

layout(std430, binding = 1) buffer MatrixB {
    float B[];
};

layout(std430, binding = 2) buffer MatrixC {
    float C[];
};

uniform uint M; // rows of A and C
uniform uint N; // cols of A, rows of B
uniform uint P; // cols of B and C

void main() {
    uint row = gl_GlobalInvocationID.y;
    uint col = gl_GlobalInvocationID.x;
    
    if (row >= M || col >= P)
        return;
    
    float sum = 0.0;
    for (uint k = 0; k < N; k++) {
        sum += A[row * N + k] * B[k * P + col];
    }
    
    C[row * P + col] = sum;
}
